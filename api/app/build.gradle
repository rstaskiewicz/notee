import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

apply plugin: 'application'
apply plugin: 'org.springframework.boot'
apply plugin: 'com.bmuschko.docker-remote-api'

mainClassName = "com.gitlab.lamapizama.notee.user.account.NoteeApplication"

dependencies {
    implementation project(':commons')
    implementation project(':user-account-web')
    implementation project(':user-account-infrastructure')
    implementation project(':note-web')
    implementation project(':note-infrastructure')

    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-configuration-processor'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.security.oauth:spring-security-oauth2:2.3.7.RELEASE'
    implementation 'org.springframework.cloud:spring-cloud-stream'
    implementation 'org.springframework.cloud:spring-cloud-stream-binder-kafka'
    //    implementation 'io.springfox:springfox-swagger2:2.9.2'
    //    implementation 'io.springfox:springfox-swagger-ui:2.9.2'
}

task createDockerfile(type: Dockerfile, dependsOn: build) {
    destFile = project.file('build/docker/Dockerfile')
    from 'openjdk:8-jre-alpine'
    label(['maintainer': 'Rafał Staśkiewicz "rstaskiewicz1@gmail.com"'])
    copyFile jar.archiveFileName.get(), "app/${jar.archiveFileName.get()}"
    entryPoint 'java'
    defaultCommand '-jar', "app/${jar.archiveFileName.get()}"
    exposePort 8080
    runCommand 'apk --update --no-cache add curl'
    instruction 'HEALTHCHECK CMD curl -f http://localhost:8080/health || exit 1'
}

task syncAppArchive(type: Sync, dependsOn: build) {
    from "build/libs/${jar.archiveFileName.get()}"
    into createDockerfile.getDestDir().get()
}

createDockerfile.dependsOn syncAppArchive

task buildDockerImage(type: DockerBuildImage, dependsOn: createDockerfile) {
    inputDir = createDockerfile.getDestDir()
    tags.add "${parent.name}:${version}"
    tags.add "${parent.name}:latest"
}
